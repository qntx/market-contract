// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;

import {EIP712} from "@openzeppelin/contracts/utils/cryptography/EIP712.sol";
import {SignatureChecker} from "@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol";
import {ERC8183} from "./ERC8183.sol";

/// @title ERC8183WithAuthorization
/// @notice EIP-712 signed authorization entrypoints over ERC8183.
/// @dev Relayers pass `auth.signer` into kernel internals. Nonces are unordered
///      and packed as `uint160(signer) << 96 | nonce`.
contract ERC8183WithAuthorization is ERC8183, EIP712 {
    bytes32 public constant CREATE_JOB_AUTHORIZATION_TYPEHASH = keccak256(
        "CreateJobAuthorization(address signer,address provider,address evaluator,uint48 expiredAt,bytes32 descriptionHash,address hook,uint256 providerAgentId,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SET_PAYOUT_RECEIVER_AUTHORIZATION_TYPEHASH = keccak256(
        "SetPayoutReceiverAuthorization(address signer,uint256 jobId,address payoutReceiver,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SET_PROVIDER_AUTHORIZATION_TYPEHASH = keccak256(
        "SetProviderAuthorization(address signer,uint256 jobId,address provider,uint256 agentId,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SET_BUDGET_AUTHORIZATION_TYPEHASH = keccak256(
        "SetBudgetAuthorization(address signer,uint256 jobId,address token,uint256 amount,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant FUND_AUTHORIZATION_TYPEHASH = keccak256(
        "FundAuthorization(address signer,uint256 jobId,address expectedToken,uint256 expectedBudget,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SUBMIT_AUTHORIZATION_TYPEHASH = keccak256(
        "SubmitAuthorization(address signer,uint256 jobId,bytes32 deliverable,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant COMPLETE_AUTHORIZATION_TYPEHASH = keccak256(
        "CompleteAuthorization(address signer,uint256 jobId,uint48 submittedAt,bytes32 reason,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant REJECT_AUTHORIZATION_TYPEHASH = keccak256(
        "RejectAuthorization(address signer,uint256 jobId,uint48 submittedAt,bytes32 reason,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SUBMIT_CLAIM_AUTHORIZATION_TYPEHASH = keccak256(
        "SubmitClaimAuthorization(address signer,uint256 jobId,uint256 cumulativeAmount,bytes32 deliverable,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant SETTLE_CLAIM_AUTHORIZATION_TYPEHASH = keccak256(
        "SettleClaimAuthorization(address signer,uint256 jobId,uint256 cumulativeAmount,bytes32 deliverable,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant APPROVE_CLAIM_AUTHORIZATION_TYPEHASH = keccak256(
        "ApproveClaimAuthorization(address signer,uint256 jobId,uint256 cumulativeAmount,bytes32 deliverable,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );
    bytes32 public constant REJECT_CLAIM_AUTHORIZATION_TYPEHASH = keccak256(
        "RejectClaimAuthorization(address signer,uint256 jobId,uint256 cumulativeAmount,bytes32 deliverable,bytes32 reason,bytes32 optParamsHash,uint72 nonce,uint256 deadline)"
    );

    /// @notice Packed nonces: `uint160(signer)` in the upper 160 bits, then 24 zero bits, then `uint72 nonce`.
    mapping(bytes32 packedNonce => bool used) public authorizationNonceUsed;

    struct Authorization {
        address signer;
        uint72 nonce;
        uint256 deadline;
        bytes sig;
    }

    struct CreateJobAuthorizationParams {
        address provider;
        address evaluator;
        uint48 expiredAt;
        string description;
        address hook;
        uint256 providerAgentId;
    }

    event AuthorizationUsed(address indexed signer, bytes32 indexed nonce);
    event AuthorizationCanceled(address indexed signer, bytes32 indexed nonce);

    error AuthorizationExpired();
    error AuthorizationNonceUsed();
    error InvalidAuthorizationSignature();

    constructor(
        uint256 platformFeeBp_,
        uint256 evaluatorFeeBp_,
        address treasury_,
        address owner_
    ) ERC8183(platformFeeBp_, evaluatorFeeBp_, treasury_, owner_) EIP712("ERC8183", "1") {}

    function DOMAIN_SEPARATOR() public view returns (bytes32) {
        return _domainSeparatorV4();
    }

    /// @notice Burns one of `msg.sender`'s nonces so a signed authorization cannot be relayed later.
    /// @dev Not relayable: only the signer can nullify their outstanding authorization.
    function cancelAuthorization(
        uint72 nonce
    ) external nonReentrant {
        bytes32 packedNonce = _packAuthorizationNonce(msg.sender, nonce);
        if (authorizationNonceUsed[packedNonce]) revert AuthorizationNonceUsed();
        authorizationNonceUsed[packedNonce] = true;
        emit AuthorizationCanceled(msg.sender, packedNonce);
    }

    function createJobWithAuthorization(
        CreateJobAuthorizationParams calldata params,
        Authorization calldata auth
    ) external nonReentrant returns (uint256) {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    CREATE_JOB_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    params.provider,
                    params.evaluator,
                    params.expiredAt,
                    keccak256(bytes(params.description)),
                    params.hook,
                    params.providerAgentId,
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        return _createJob(
            auth.signer,
            params.provider,
            params.evaluator,
            params.expiredAt,
            params.description,
            params.hook,
            params.providerAgentId
        );
    }

    function setPayoutReceiverWithAuthorization(
        uint256 jobId,
        address payoutReceiver,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SET_PAYOUT_RECEIVER_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    payoutReceiver,
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _setPayoutReceiver(auth.signer, jobId, payoutReceiver);
    }

    function setProviderWithAuthorization(
        uint256 jobId,
        address provider_,
        uint256 agentId,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SET_PROVIDER_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    provider_,
                    agentId,
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _setProvider(auth.signer, jobId, provider_, agentId);
    }

    function setBudgetWithAuthorization(
        uint256 jobId,
        address token,
        uint256 amount,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SET_BUDGET_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    token,
                    amount,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _setBudget(auth.signer, jobId, token, amount, optParams);
    }

    function fundWithAuthorization(
        uint256 jobId,
        address expectedToken,
        uint256 expectedBudget,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    FUND_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    expectedToken,
                    expectedBudget,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _fund(auth.signer, jobId, expectedToken, expectedBudget, optParams);
    }

    function submitWithAuthorization(
        uint256 jobId,
        bytes32 deliverable,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SUBMIT_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    deliverable,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _submit(auth.signer, jobId, deliverable, optParams);
    }

    function completeWithAuthorization(
        uint256 jobId,
        bytes32 reason,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        // Bind stored `submittedAt` so a pre-submit signature cannot apply after submit.
        uint48 submittedAt = _jobs[jobId].submittedAt;
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    COMPLETE_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    submittedAt,
                    reason,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _complete(auth.signer, jobId, reason, optParams);
    }

    function rejectWithAuthorization(
        uint256 jobId,
        bytes32 reason,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        uint48 submittedAt = _jobs[jobId].submittedAt;
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    REJECT_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    submittedAt,
                    reason,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _reject(auth.signer, jobId, reason, optParams);
    }

    function submitClaimWithAuthorization(
        uint256 jobId,
        uint256 cumulativeAmount,
        bytes32 deliverable,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SUBMIT_CLAIM_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    cumulativeAmount,
                    deliverable,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _submitClaim(auth.signer, jobId, cumulativeAmount, deliverable, optParams);
    }

    function settleClaimWithAuthorization(
        uint256 jobId,
        uint256 cumulativeAmount,
        bytes32 deliverable,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    SETTLE_CLAIM_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    cumulativeAmount,
                    deliverable,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _settleClaim(auth.signer, jobId, cumulativeAmount, deliverable, optParams);
    }

    function approveClaimWithAuthorization(
        uint256 jobId,
        uint256 cumulativeAmount,
        bytes32 deliverable,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    APPROVE_CLAIM_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    cumulativeAmount,
                    deliverable,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _approveClaim(auth.signer, jobId, cumulativeAmount, deliverable, optParams);
    }

    function rejectClaimWithAuthorization(
        uint256 jobId,
        uint256 cumulativeAmount,
        bytes32 deliverable,
        bytes32 reason,
        bytes calldata optParams,
        Authorization calldata auth
    ) external nonReentrant {
        _verifyAuthorization(
            auth.signer,
            auth.nonce,
            auth.deadline,
            keccak256(
                abi.encode(
                    REJECT_CLAIM_AUTHORIZATION_TYPEHASH,
                    auth.signer,
                    jobId,
                    cumulativeAmount,
                    deliverable,
                    reason,
                    keccak256(optParams),
                    auth.nonce,
                    auth.deadline
                )
            ),
            auth.sig
        );
        _rejectClaim(auth.signer, jobId, cumulativeAmount, deliverable, reason, optParams);
    }

    /// @dev Mark the nonce used before ERC-1271 verification so a reentrant signer cannot replay.
    function _verifyAuthorization(
        address signer,
        uint72 nonce,
        uint256 deadline,
        bytes32 structHash,
        bytes calldata sig
    ) internal {
        if (block.timestamp > deadline) revert AuthorizationExpired();
        bytes32 packedNonce = _packAuthorizationNonce(signer, nonce);
        if (authorizationNonceUsed[packedNonce]) revert AuthorizationNonceUsed();
        authorizationNonceUsed[packedNonce] = true;
        bytes32 digest = _hashTypedDataV4(structHash);
        if (!SignatureChecker.isValidSignatureNowCalldata(signer, digest, sig)) {
            revert InvalidAuthorizationSignature();
        }
        emit AuthorizationUsed(signer, packedNonce);
    }

    function _packAuthorizationNonce(
        address signer,
        uint72 nonce
    ) internal pure returns (bytes32) {
        return bytes32((uint256(uint160(signer)) << 96) | uint256(nonce));
    }
}
